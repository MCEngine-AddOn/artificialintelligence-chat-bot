plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

version = project.properties[project.name + '.version'] ?: 'undefined'
group = 'io.github.mcengine'

ext {
    git_user_name = System.getenv('GIT_USER_NAME') ?: 'null'
    git_user_token = System.getenv('GIT_USER_TOKEN') ?: 'null'
    jarName = System.getenv('PROJECT_NAME') ?: 'MCEngine-Chat-Bot'
}

def mcengineApiVersion = findProperty('mcengine-api.version') ?: 'undefined'
def mcengineArtificialIntelligenceApiVersion = findProperty('mcengine-artificialintelligence-api.version') ?: 'undefined'
def spigotmcApiVersion = findProperty('spigotmc-api.version') ?: 'undefined'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://oss.sonatype.org/content/repositories/central' }
    maven {
        url = uri('https://maven.pkg.github.com/MCEngine/mcengine')
        credentials {
            username = git_user_name
            password = git_user_token
        }
    }
    maven {
        url = uri('https://maven.pkg.github.com/MCEngine/artificialintelligence')
        credentials {
            username = git_user_name
            password = git_user_token
        }
    }
}

dependencies {
    implementation project(path: ":mcengine-chat-bot-api", configuration: 'shadow')
    compileOnly "io.github.mcengine:mcengine-api:$mcengineApiVersion"
    compileOnly "io.github.mcengine:mcengine-artificialintelligence-api:$mcengineArtificialIntelligenceApiVersion"
    compileOnly "org.spigotmc:spigot-api:$spigotmcApiVersion"
}

shadowJar {
    archiveClassifier = null
    archiveBaseName = project.jarName
}

tasks.named('jar').configure {
    enabled = false
}

build.dependsOn(shadowJar)
